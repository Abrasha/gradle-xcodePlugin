plugins {
	id "com.gradle.plugin-publish" version "0.9.1"
	id 'net.saliman.cobertura' version '2.3.1'
	id 'com.github.kt3k.coveralls' version '2.6.3'
}


def publishURL = System.getenv("PUBLISH_URL")
def publishUser = System.getenv("PUBLISH_USER")
def publishPassword = System.getenv("PUBLISH_PASSWORD")

if (project.hasProperty("publishURL")) {
	publishURL = project.publishURL
}
if (project.hasProperty("publishUser")) {
	publishUser = project.publishUser
}
if (project.hasProperty("publishPassword")) {
	publishPassword = project.publishPassword
}



cobertura.coverageFormats = ['html', 'xml']

apply plugin: 'maven'

repositories {
	mavenCentral()
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}

configurations {
	deployerJars
}



dependencies {
	compile gradleApi()
	compile 'org.apache.httpcomponents:httpclient:4.4.1'
	compile 'org.apache.httpcomponents:httpmime:4.4.1'
	compile 'org.pegdown:pegdown:1.5.+'
	compile 'org.openbakery.coverage:CoverageReport:0.9.3'
	deployerJars 'org.apache.maven.wagon:wagon-ssh:2.10'
}

jar {
	baseName 'xcode-plugin'
	from project.sourceSets.main.allSource
	from project(":libxcode").sourceSets.main.allSource
	//from configurations.includeInJar.collect { it.isDirectory() ? it : zipTree(it) }
}

uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			
			repository(url: publishURL) {
				authentication(userName: publishUser, password: publishPassword)
			}
			
			pom.project {
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				developers {
					developer {
						id "renep"
						name "Ren√© Pirringer"
						email "rene@openbakery.org"
					}
				}
			}
		}
	}
}


task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}


pluginBundle {
	website = 'http://openbakery.org/gradle.html'
	vcsUrl = 'https://github.com/openbakery/gradle-xcodePlugin'
	description = 'A gradle plugin for building Xcode projects!'
	tags = ['xcode', 'iOS', 'os x', "osx", "mac", "iphone", "ipad"]

	plugins {
		xcodePlugin {
			id = 'org.openbakery.xcode-plugin'
			displayName = 'Gradle Xcode plugin'
		}
	}
}
